apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'maven-publish'

version = "0.1.3"
android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "SDK_VERSION", "\"${version}\"")
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "SDK_VERSION", "\"${version}\"")
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    compileOnly 'com.sensorsdata.analytics.android:SensorsAnalyticsSDK:6.0.0'
}

afterEvaluate {
    assembleRelease {
        doLast {
            copy {
                def rootPath = getProjectDir().getAbsolutePath()
                from(rootPath + '/build/outputs/aar/')
                into(rootPath + '/build/outputs/aar/')
                rename('SensorsABTestSDK-release.aar', 'SensorsABTestSDK-' + version + '.aar')
            }
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    dependsOn 'assembleRelease'
    source = android.sourceSets.main.java.srcDirs +
            'build/generated/not_namespaced_r_class_sources/release/generateReleaseRFile/out/com/sensorsdata/analytics/android/sdk/R.java'
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            groupId = 'com.sensorsdata.analytics.abtesting'
            artifactId = 'SensorsABTesting'
            version = version
            artifact sourceJar
            artifact javadocJar
            artifact('build/outputs/aar/SensorsABTestSDK-release.aar')
            // 配置 pop 文件格式
            pom {
                packaging 'aar'
                name = 'The official Android SDK for Sensors A/B Testing.'
                description = 'The official Android SDK for Sensors A/B Testing.'
                url = 'The official Android SDK for Sensors A/B Testing.'
                licenses {
                    license {
                        name='The Apache Software License, Version 2.0'
                        url='http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dengshiwei'
                        name = 'dengshiwei'
                        email = 'dengshiwei@sensorsdata.com'
                    }
                }
                scm {
                    connection = 'https://github.com/sensorsdata/abtesting-sdk-android'
                    developerConnection = 'https://github.com/sensorsdata/abtesting-sdk-android.git'
                    url = 'https://github.com/sensorsdata/abtesting-sdk-android'
                }
            }
        }
    }
    // 配置远程仓库
    repositories {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def mavenUser = properties.getProperty("maven.user")
        def mavenPassword = properties.getProperty("maven.password")
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
    }
}

// 对 mavenAndroid 发布内容进行签名
signing {
    sign publishing.publications.mavenAndroid
}